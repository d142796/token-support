@Library('deploy')
import deploy

def deployLib = new deploy()

node {
    def commitHash, commitHashShort, commitUrl
    def repo = "navikt"
    def app = "token-support"
    def committer, committerEmail, changelog, releaseVersion
    def mvnHome = tool "maven-3.3.9"
    def mvn = "${mvnHome}/bin/mvn"

    stage("Initialization") {
        cleanWs()
        withCredentials([string(credentialsId: 'OAUTH_TOKEN', variable: 'token')]) {
           withEnv(['HTTPS_PROXY=http://webproxy-utvikler.nav.no:8088']) {
            sh(script: "git clone https://${token}:x-oauth-basic@github.com/${repo}/${app}.git .")
           }
         }
        commitHash = sh(script: 'git rev-parse HEAD', returnStdout: true).trim()
        commitHashShort = sh(script: 'git rev-parse --short HEAD', returnStdout: true).trim()
        commitUrl = "https://github.com/${repo}/${app}/commit/${commitHash}"
        committer = sh(script: 'git log -1 --pretty=format:"%an"', returnStdout: true).trim()
        committerEmail = sh(script: 'git log -1 --pretty=format:"%ae"', returnStdout: true).trim()
        changelog = sh(script: 'git log `git describe --tags --abbrev=0`..HEAD --oneline', returnStdout: true)

        notifyGithub(repo, app, 'continuous-integration/jenkins', commitHash, 'pending', "Build #${env.BUILD_NUMBER} has started")

        //releaseVersion = "${env.major_version}.${env.BUILD_NUMBER}-${commitHashShort}"
    }

    stage("Minor release") {
		
		def pom = readMavenPom file: 'pom.xml'
		def ver = pom.version
		println "current version is " + ver
		script {
			if (!env.releaseLabel?.trim()) {
				releaseVersion = ver.replace('-SNAPSHOT','')
			} else {
				releaseVersion = ver.replace('-SNAPSHOT','') + "-${env.releaseLabel}"
			}
		}
		
        sh "${mvn} versions:set -B -DnewVersion=${releaseVersion}"
        sh "${mvn} clean deploy -Djava.io.tmpdir=/tmp/${app} -B -e"          
    }

    stage("Tag & Update dev version") {
        
        withEnv(['HTTPS_PROXY=http://webproxy-utvikler.nav.no:8088']) {
            withCredentials([string(credentialsId: 'OAUTH_TOKEN', variable: 'token')]) {
				sh ("git tag -a ${releaseVersion} -m ${releaseVersion}")
                sh ("git push https://${token}:x-oauth-basic@github.com/${repo}/${app}.git --tags")
            }
        }
		def devVersion;
		
		script {
			if (!env.releaseLabel?.trim()) {
				devVersion = incrementVersion(releaseVersion, "")
			} else {
				devVersion = incrementVersion(releaseVersion, "-${env.releaseLabel}")
			}
		}
		
		sh "${mvn} versions:set -B -DnewVersion=${devVersion}-SNAPSHOT"
		
		withEnv(['HTTPS_PROXY=http://webproxy-utvikler.nav.no:8088']) {
			withCredentials([string(credentialsId: 'OAUTH_TOKEN', variable: 'token')]) {
				sh ("git commit -a -m ${devVersion}")
				sh ("git push https://${token}:x-oauth-basic@github.com/${repo}/${app}.git origin master")
			}
		}
		
        slackSend([
                color: 'good',
                message: "Build <${env.BUILD_URL}|#${env.BUILD_NUMBER}> (<${commitUrl}|${commitHashShort}>) of ${repo}/${app}@master by ${committer} passed  (${changelog})"
        ])
		notifyGithub(repo, app, 'continuous-integration/jenkins', commitHash, 'success', "Build #${env.BUILD_NUMBER} has finished")
    }
}

def notifyGithub(owner, app, context, sha, state, description) {
    def postBody = [
            state: "${state}",
            context: "${context}",
            description: "${description}",
            target_url: "${env.BUILD_URL}"
    ]
    def postBodyString = groovy.json.JsonOutput.toJson(postBody)

    withEnv(['HTTPS_PROXY=http://webproxy-utvikler.nav.no:8088']) {
        withCredentials([string(credentialsId: 'OAUTH_TOKEN', variable: 'token')]) {
            sh """
                curl -H 'Authorization: token ${token}' \
                    -H 'Content-Type: application/json' \
                    -X POST \
                    -d '${postBodyString}' \
                    'https://api.github.com/repos/${owner}/${app}/statuses/${sha}'
            """
        }
    }
}

def incrementVersion(version, postfixLabel){
	def ver = version.replace(postfixLabel,"")
	def parts = ver.tokenize(".")
	def number = parts.pop() as int
	parts.push(number + 1)
	def newVersion = parts.join(".")
	return newVersion
}
